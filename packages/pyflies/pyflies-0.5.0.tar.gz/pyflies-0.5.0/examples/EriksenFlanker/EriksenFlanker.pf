"""
Eriksen flanker task
--------------------

Eriksen, B. A.; Eriksen, C. W. (1974). "Effects of noise letters upon
identification of a target letter in a non- search task". Perception and
Psychophysics. 16: 143â€“149.

"""

directions = [left, right]
congruencies = [incongruent, congruent]
repeats = 1

test EriksenFlanker {

    | repeat          | direction       | category          |
    | --------------- | --------------- | ----------------- |
    | 1..repeats loop | directions loop | congruencies loop |


    fix -> cross() for 1000..3000 choose

    exec -> image(file "images/{{direction}}-{{category}}.png", size 100)
            keyboard(valid directions, correct direction)

    correct -> circle(fillColor green, color green, radius 100) for 300
    error -> circle(fillColor red, color green, radius 100) for 300
    //error -> sound(freq 300) for 500
    //correct -> sound(freq 700) for 300

}

screen intro {
    Welcome
    -------

    In the following test you will be presented with a line of 5 arrow pointing
    left or right. You should respond by the direction of the *middle arrow* as
    fast as possible by pressing left or right arrow on the keyboard or
    touching/clicking on appropriate button on the screen.

    You will first do a practice run with 4 trials. After that a real run with
    20 trial will be performed.

    Press SPACE or touch/click the screen to continue
}

screen real {
    Real block
    ----------
    
    Now a real block of trials will be performed.

    Press SPACE or touch/click the screen to continue
}



flow {

  // Show introduction
  show intro

  // Perform practice test serie
  execute EriksenFlanker(practice true, random true)

  show real 
  
  // Perform real test serie (collects data)
  execute EriksenFlanker(repeats 5, random true)

}

target PsychoPy {
//  soundBackend = 'backend_sounddevice.SoundDeviceSound'
}
