"""
This example shows how to create blocks of trials

It is based on PsychoPy tutorial:
https://www.psychopy.org/builder/blocksCounterbalance.html

We have blocks of trials where we display images to the subject. Each block will
show either images of houses or faces. Each block will be repeated 3 times with
10 trial with a short break in between.

To generate log and investigate the flow of the experiment do:

   textx generate blocking.pf --overwrite --target log

To generate PsychoPy script do:

   textx generate blocking.pf --overwrite --target psychopy

"""

image_types = [houses, faces]
blocks_number = 3

test showImages {

    // images will be stored in folders "houses" and "faces"
    // In each folder images will be named 1.png, 2.png ... 10.png
    | trial_no   | image                             |
    | ---------- | --------------------------------- |
    | 1..10 loop | "{{image_type}}/{{trial_no}}.png" |

    fix ->  cross() for 400..1000 choose
    exec -> image(file image)
            keyboard()
}

screen instruction {
    Block set {{order}}
    You will be presented with images of {{image_type}}
}

screen break {
    Block {{repeat_index}} finished.
    Take a short break.
}

flow {
    repeat {
        show instruction
        
        // 3 same blocks
        repeat blocks_number times {
            execute showImages
            show break for 1000
        }
    } with
    | image_type       | order |
    | ---------------- | ----- |
    | image_types loop | 1..2  |

}